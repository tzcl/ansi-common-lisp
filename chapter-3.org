#+TITLE: Chapter 3
#+SUBTITLE: Lists

* Notes
+ "While =eql= returns true only if its arguments are the same object, =equal= essentially, returns true if its arguments would print the same."
  + [[https://eli.thegreenplace.net/2004/08/08/equality-in-lisp][Equality in Lisp]]
    + ===  compares only numbers (which may be of different types)
    + =eq= compares symbols, two objects are =eq= iff they are actually the same object in memory (don't use for numbers or characters)
    + =eql= is the default equality test, it is mostly like =eq= but works on numbers (type sensitive) and characters (case sensitive)
    + =equal= compares general objects, two objects are =equal= iff they are =eql=, strings of =eql= characters or lists of =equal= objects (for anything else, =eq= is used)
    + =equalp= is like =equal= but more lenient, comparison of numbers is type insensitive, comparison of strings  is case insensitive, and objects are =equalp= if their members are =equalp= (for anything else, =eq= is used)
+ "The reason Lisp has no pointers is that every value is conceptually a pointer. When you assign a value to a variable or store it in a data structure, what gets stored is actually a pointer to the value."
+ "By default, =member= compares objects using =eql=. You can override this default by using a /keyword/ argument."
  + A keyword argument is of the form, =:test=, for example. We'll see later that keywords relate to namespaces somehow. We use them to refer to special arguments.
+ "You have to be careful when using =sort=, because it's /destructive/. For efficiency reasons, =sort= is allowed to modify the sequence given to it as an argument."
+ "A cons that isn't a proper list is called a /dotted list/. This is not a very good name, because conses that aren't proper lists are usually not meant to represent lists at all =(a . b)= is just some kind of pair."
+ "A list of conses is called an /assoc-list/ or /alist/. Assoc-lists are slow, but convenient in the first stages of a program: =((+ . "add") (- . "sub"))=."
  + They can be optimised by converting to hashmaps
+ "The globals =*=,  =**=, =***= are always set to the last three values returned to the toplevel. These variables are useful in debugging."
+ "Consing will always cost something, and in some existing Lisp systems, it is quite expensive." "You can avoid some of this consing by using destructive functions, which try to re-use most of the structure of the lists passed to them as arguments."
+ "Run-length encoding is a simple compression algorithm for use in restaurants" -- LOL

* Exercises
My goal is to post all of my solutions online, therefore, I should bother to do all the exercises. Need to workout how to make the nice box-and-pointer diagrams.

One way to make diagrams is to use TikZ. Paul Graham says he used something called "Idraw" to create the diagrams in the text. I can't be bothered doing this so I just drew the diagram on my iPad. Need to work out how to export images nicely.

Q1: [[https://i.imgur.com/pSbrSB4.png]]

Q2: Write a version of =union= that preserves the order of the elements in the original lists:
#+begin_src lisp
(defun new-union (list1 list2)
  (let ((rlist1 (reverse list1)))
    (dolist (e list2 (nreverse rlist1))
      (pushnew e rlist1))))
#+end_src

Q3: Define a function that takes a list and returns a list indicating the number of times each (=eql=) element appears, sorted from most common element to least common:
#+begin_src lisp
(defun freq (xs)
  ;; Optimise by using a hashmap
  (let* ((uniq (remove-duplicates xs))
         (alist (pairlis uniq (make-list (length uniq) :initial-element 0))))
    (loop for x in xs
          do (incf (cdr (assoc x alist))))
    (sort alist #'> :key #'cdr)))
#+end_src

Q4: Why does ~(member '(a) '((a) (b)))~ return =nil=?
+ It returns =nil= because =member= uses =eql= as the default equality comparison, which doesn't work on lists. To get this to work as expected, we need to tell =member= to use =equal=: ~(member '(a) '((a) (b)) :test #'equal)~.

Q5: Create a function =pos+= that takes a list and returns a list of each element plus its position:
#+begin_src lisp
(defun pos+ (xs &optional (index 0))    ; recursive
  (if (null xs) '()
      (cons (+ (car xs) index)
            (pos+ (cdr xs) (1+ index)))))

(defun pos+ (xs)                        ; iterative
  (loop for x in xs
        for i from 0
        collect (+ x i)))

(defun range (max &key (min 0) (step 1))
  (loop for i from min below max by step
        collect i))

(defun pos+ (xs)                        ; mapcar
  (mapcar #'+ xs (range (length xs))))  ; zipWith is a common pattern in FP
#+end_src

Q6: Suppose the government swaps the representation of a list such that the =car= refers to the  and =cdr=. Then, we would define =cons=, =list=, =length= and =member= as:
#+begin_src lisp
(defun gov-cons (x y) (cons y x))

(defun gov-list (xs)
  (if (null xs) nil
      (gov-cons (car xs) (gov-list (cdr xs)))))

(defun gov-length (xs)
  (if (null xs) 0
      (+ 1 (gov-length (car xs)))))

(defun gov-member (x xs)
  (and (consp xs)
       (or (eql x (cdr xs))
           (gov-member x (car xs)))))
#+end_src

Q7: Optimise the run-length encoding program to use fewer =cons= cells (by using dotted lists):
#+begin_src lisp
(defun compress (x)
  (if (consp x)
      (compr (car x) 1 (cdr x))
      x))

(defun compr (x n xs)
  (if (null xs)
      (list (n-elts x n))
      (let ((next (car xs)))
        (if (eql next x)
            (compr x (1+ n) (cdr xs))
            (cons (n-elts x n)
                  (compr next 1 (cdr xs)))))))

(defun n-elts (x n)
  ;; using a dotted list instead of a proper list saves a cons per item
  (if (> n 1) (cons n x) x))

(defun uncompress (xs)
  (if (null xs) '()
      (let ((x (car xs))
            (rest (uncompress (cdr xs))))
        (if (atom x)
            (cons x rest)
            (append (make-list (car x) :initial-element (cdr x))
                    rest)))))
#+end_src

Q8: Define a function that takes a list and prints it in dot notation:
#+begin_src lisp
(defun showdots (xs)
  (format t "~a" (showdots-recur xs)))

(defun showdots-recur (xs)
  (if (null xs) 'nil
      (format nil "(~a . ~a)" (car xs) (showdots-recur (cdr xs)))))
#+end_src

Q9: Write a program to find the /longest/ finite path through a network represented as in Section 3.15. The network may contain cycles. In general, finding the longest path is an NP-hard problem. My approach was to find all paths between the start and the end using a DFS and selecting the longest.
#+begin_src lisp
(defun longest-path (start end net)
  (dfs end net `((,start)) '(0 . nil)))

(defun new-paths (path node net)
  (loop for n in (cdr (assoc node net))
        when (not (member n path))
          collect (cons n path)))

(defun longer (old new)
  (let ((old-len (car old))             ; save recomputing the length
        (new-len (length new)))
    (if (< old-len new-len) (cons new-len (reverse new)) old)))

(defun dfs (end net stack longest)
  (if (null stack) (cdr longest)
      (let* ((path (pop stack))
             (node (car path)))
        (dfs end net
             (append (new-paths path node net) stack)
             (longer longest path)))))
#+end_src
